; Copyright 2003 Tematic Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
Mask    *       &FFFFFC00

SCSILoader
        B       ReadByte
        B       WriteByte
        B       Reset
        BICS    PC,R14,#V_bit           ; Call loader

10
        DCD     0

Reset
        MOV     R10,#IRQsema
        LDR     R10,[R10]
        TEQ     R10,#0
        BNE     ReturnIRQError
        LDR     R10,=Mask
        AND     R10,R11,R10
        ADD     R10,R10,#&3000
        LDR     R2,&10
        STRB    R2,[R10]
        BICS    PC,LR,#V_bit

ReturnIRQError
        ADR     R0,Error_NotIRQ
        ORRS    PC,LR,#V_bit

ReadByte
        MOV     R10,#IRQsema
        LDR     R10,[R10]
        TEQ     R10,#0
        BNE     ReturnIRQError
        LDR     R4,=Mask
        AND     R4,R11,R4
        ADD     R10,R4,#&3000
        CMP     R1,#&F800
        ADRHS   R0,Error_Address
        ORRCSS  PC,LR,#V_bit
        ADD     R2,R1,#&800
        MOV     R2,R2,ASR #11
        LDR     R3,&10
        TEQP    PC,#I_bit+SVC_mode
        ORR     R2,R2,R3
        STRB    R2,[R10]
        BIC     R2,R1,#&3F800
        LDRB    R0,[R4,R2,LSL #2]
        BICS    PC,R14,#V_bit

WriteByte
        ADR     R0,Error_Write
        ORRS    PC,R14,#V_bit


98      CMP     R0,#RCsoftcopyadr
        BICEQ   R1,R1,#&3F
        STREQ   R1,&10
        BICS    PC,R14,#V_bit

Error_Write
        DCD     &580
        DCB     "This podule doesn't support writeable devices", 0
        ALIGN

Error_Address
        DCD     &584
        DCB     "Address too big", 0
        ALIGN

Error_NotIRQ
        DCD     &20111
        DCB     "Not callable from IRQ routine", 0
        ALIGN

        DCB     "SCSICardLoader",9
        DCB     "1.04 (11 Jul 89)", 0
        ALIGN

        LTORG

        END
