; Copyright 2002 Tematic Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
RAMAlloc
        Push    "r2-r4,lr"
        LDR     lr, mempool_base_log
        TEQ     lr, #0
        BLEQ    InitDA
        Pull    "r2-r4,pc",VS
 [ DebugMemory
        DREG    r0, "Requested size ",cc
        DREG    r1, ", alignment ",cc
        DREG    r2, ", boundary limitation "
 ]
        CMP     r2, r1
        MOVLO   r2, r1          ; cheaty cheaty
        LDR     r1, mempool_base_log
        MOV     r4, r2
        CMP     r4, #4
        ADDLS   r3, r0, #4
        ADDHI   r3, r0, r4
        MOV     r0, #2
        SWI     XOS_Heap
        BVC     GotAlloced
        LDR     r14, [r0]
        TEQ     r14, #ErrorNumber_HeapFail_Alloc
        Pull    "r2-r4,pc",NE
        ; Grow heap
        LDR     r0, [r1, #8]    ; heap base offset
        LDR     r1, [r1, #12]   ; heap end offset
        SUB     r1, r1, r0      ; bytes free
        SUB     r1, r3, r1      ; bytes needed
        ADD     r1, r1, #8      ; safety factor

        LDR     r0, mempool_da_number
        SWI     XOS_ChangeDynamicArea

        MOVVC   r0, #2
        LDRVC   r1, mempool_base_log
        SWIVC   XOS_Heap
        Pull    "r2-r4,pc",VS


GotAlloced
 [ DebugMemory
        DREG    r2, "Allocated block at ",cc
        DREG    r3, ", size = "
 ]
        ADD     r0, r2, #4
        CMP     r4, #4
        SUBHI   r3, r4, #1
        ADDHI   r0, r2, r3
        BICHI   r0, r0, r3
        STR     r2, [r0, #-4]
        LDR     r3, mempool_base_phys
        SUB     lr, r0, r1
        ADD     r1, r3, lr
 [ DebugMemory
        DREG    r0, "Returned logical address = ",cc
        DREG    r1, ", physical address = "
 ]
        Pull    "r2-r4,pc"

RAMFree
        Push    "r1,r2,lr"
        LDR     r2, [r0, #-4]
        LDR     r1, mempool_base_log
        MOV     r0, #3
        SWI     XOS_Heap
        Pull    "r1,r2,pc"

InitDA
        Push    "r0-r8,lr"
        MOV     r3, #0
        STR     r3, mempool_base_log
        BL      open_message_file
        ADRVC   r0, message_file_block
        ADRVC   r1, Token_DAName
        ADRVC   r2, name_buffer
        MOVVC   r3, #?name_buffer
        MOVVC   r4, #0
        MOVVC   r5, #0
        MOVVC   r6, #0
        MOVVC   r7, #0
        SWIVC   XMessageTrans_Lookup
        MOVVC   r0, #0
        STRVCB  r0, [r2, r3]
        MOVVC   r8, r2
        SWIVC   XOS_ReadMemMapInfo
        MOVVC   r2, r0                  ; initial size = 1 page
        MOVVC   r0, #0
        MOVVC   r1, #-1
        STRVC   r1, mempool_base_ppn
        MOVVC   r3, #-1
        MOVVC   r4, #2_00100010         ; SVC only, B, ~C, draggable
        ORRVC   r4, r4, #&100           ; requires specific physical pages
        MOVVC   r5, #32*1024*1024
        ADRVC   r6, DynAreaHandler
        MOVVC   r7, wp
        SWIVC   XOS_DynamicArea
 [ DebugMemory
        BVS     %FT01
        DREG    r1, "Dynamic area ",,Integer
        DREG    r3, "Logical address "
01
 ]
        STRVS   r0, [sp]
        Pull    "r0-r8,pc",VS
20      STR     r1, mempool_da_number
        STR     r3, mempool_base_log
        STR     r3, mempool_free
        MOV     r0, #0
        MOV     r1, r3
        MOV     r3, r2
        SWI     XOS_Heap
        Pull    "r0-r8,pc",VC
; error case
        STR     r0,[sp]
        BL      KillDA
        SETV
        Pull    "r0-r8,pc"

KillDA
        Push    "r0-r1,lr"
        MOV     r0, #1
        LDR     r1, mempool_da_number
        CMP     r1, #0
 [ DebugMemory
        BEQ     %FT01
        DREG    r1, "Removing dynamic area ",,Integer
01
 ]
        SWINE   XOS_DynamicArea
        MOV     r1, #0
        STR     r1, mempool_da_number
        STR     r1, mempool_base_log
        STRVS   r0, [sp]
        Pull    "r0-r1,pc"

Token_DAName
        =       "DAName", 0
        ALIGN

DynAreaHandler
        CMP     r0, #4
        ADDCC   pc, pc, R0, LSL #2
        B       UnknownHandlerError
        B       PreGrow
        B       PostGrow
        B       PreShrink
        B       PostShrink

UnknownHandlerError
        ADRL    r0, ErrorBlock_UnknownAreaHandler
        B       copy_error_zero

;Pregrow entry parameters
; R0 = 0 (reason code)
; R1 -> page block (entries set to -1)
; R2 = number of entries in page block == number of pages area is growing by
; R3 = number of bytes area is growing by (r2 * pagesize)
; R4 = current size (bytes)
; R5 = page size
;
; exit with V clear, all preserved

PreGrow Entry   "r0-r3"

        LDR     r3, mempool_base_ppn
        CMP     r3, #-1
        BNE     %FT05

        MOV     r1, #32*1024*1024               ; Try for 32M, 4M aligned
        MOV     r2, #22
02      MOV     r0, #12
        SWI     XOS_Memory
        BVC     %FT03
        MOV     r1, r1, LSR #1                  ; If we can't get it, halve both
        SUB     r2, r2, #1                      ; until down to 32K, 4K aligned
        TST     r1, #16*1024                    ; preserve V flag in loop test
        BEQ     %BT02
        B       %FT99
03
 [ DebugMemory
        DREG    r3, "PPN suggested ",,Integer
 ]
        STR     r3, mempool_base_ppn
        MOV     r0, #2_100001:SHL:8
        ADR     r1, mempool_base_ppn
        MOV     r2, #1
        SWI     XOS_Memory
        BVS     %FT99
 [ DebugMemory
        LDR     r0, mempool_base_phys
        DREG    r0, "Physical address "
 ]

        LDMFD   sp, {r0-r2}             ; recover input parameters

05      MOV     lr, #0
 [ DebugMemory
        DREG    r4, "Current size ",cc
        DREG    r5, ", page size "
 ]
10      CMP     lr, r4
        ADDLO   r3, r3, #1
        ADDLO   lr, lr, r5
        BLO     %BT10

20      STR     r3, [r1], #12
 [ DebugMemory :LAND: {FALSE}
        DREG    r3, "Requesting page ",,Integer
 ]
        SUBS    r2, r2, #1
        ADDNE   r3, r3, #1
        BNE     %BT20

99      STRVS   r0, [sp]
        EXIT

PostGrow
PostShrink
        LDR     r12, mempool_base_log
        TEQ     r12, #0
        STRNE   r4, [r12, #12]
        MOV     pc, lr

PreShrink
        LDR     r12, mempool_base_log
        TEQ     r12, #0
        MOVEQ   pc, lr
        Push    "r0, lr"
        PHPSEI                          ; disable IRQs round this bit
        LDR     r0, [r12, #8]           ; get minimum size
        SUB     r0, r4, r0              ; r0 = current-minimum = max shrink
        CMP     r3, r0                  ; if requested shrink > max
        MOVHI   r3, r0                  ; then limit it
        SUB     r0, r5, #1              ; r0 = page mask
        BIC     r3, r3, r0              ; round size change down to page multiple
        SUB     r0, r4, r3              ; area size after shrink
        STR     r0, [r12, #12]          ; update size

        PLP                             ; restore IRQ status
        CLRV
        Pull    "r0, pc"

        END
